' 1. open pptx
' 2. alt + F11
' 3. insert standard module
' 4. paste this script & edit it
' 5. exec macro (F5)

' edit here : depends on your system
Public Const PATH_SEPARATOR As String = "\"
' edit here : XXXX + [number].gif
Public Const OUTPUT_FILE_PREFIX As String = "sec-"
' edit here : path to ImageMagick
Public Const IMAGE_MAGICK As String = """C:\Program Files\ImageMagick-7.1.1-Q16-HDRI\convert.exe"""
Public Const IMAGE_MAGICK_PARAMS As String = "-delay 100 -loop 0 -layers Optimize"

Sub moveToFilePath()
	Dim path As String
	path = ActivePresentation.FullName
	If path <> "" Then
		ChDir Left(path, InStrRev(path, PATH_SEPARATOR))
	Else
		MsgBox "There is not saved file"
	End If
End Sub

Sub createAnimationGif(in_gif As String, in_path As String)
	Dim fso As Object
	Dim folder As Object
	Dim file As Object
	Dim commandArray() As String
	Dim ix As Integer

	Set fso = CreateObject("Scripting.FileSystemObject")
	Set folder = fso.GetFolder(in_path)

	' build command
	ReDim commandArray(folder.Files.Count - 1 + 3)
	commandArray(0) = IMAGE_MAGICK
	commandArray(1) = IMAGE_MAGICK_PARAMS
	ix = 2
	For Each file In folder.Files
		commandArray(ix) = in_path & PATH_SEPARATOR & file.Name
		ix = ix + 1
	Next file
	commandArray(ix) = in_gif

	' exec command
	Dim shell As Object
	Set shell = CreateObject("WScript.Shell")
	shell.Run Join(commandArray, " "), 0, True
End Sub

Sub main()
	Dim fso As Object
	Dim pptx As Presentation

	moveToFilePath
	Set fso = CreateObject("Scripting.FileSystemObject")
	Set pptx = ActivePresentation

	For section = 1 To pptx.SectionProperties.Count
		Dim ix_s As Integer
		Dim ix_e As Integer
		Dim path As String
		Dim agif As String
		Dim page As Integer
		
		ix_s = pptx.SectionProperties.FirstSlide(section)
		ix_e = ix_s + pptx.SectionProperties.SlidesCount(section) - 1
		path = "." & PATH_SEPARATOR & section
		agif = "." & PATH_SEPARATOR & OUTPUT_FILE_PREFIX & section & ".gif"

		' re-make only deleted Animation GIF
		If Dir(agif) <> "" Then
			GoTo ContinueLoop
		End If

		' initialize
		If fso.FolderExists(path) Then
			Dim folder As Object
			Dim file As Object
			Set folder = fso.GetFolder(path)
			For Each file In folder.Files
				file.Delete Force:=True
			Next file
		Else
			fso.CreateFolder path
		End If

		' pptx to PNG24
		For page = ix_s To ix_e
			pptx.Slides(page).Export path & PATH_SEPARATOR & page & ".png", "PNG", 1600
		Next page

		' PNG24 to Animation GIF
		createAnimationGif agif, path

		' finalize
		fso.DeleteFolder path, True

		ContinueLoop:
	Next section
End Sub
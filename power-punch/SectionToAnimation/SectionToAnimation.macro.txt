' 1. open pptx
' 2. alt + F11
' 3. insert standard module
' 4. paste this script & edit it
' 5. exec macro (F5)

' edit here : depends on your system
Public Const PATH_SEPARATOR As String = "\"
' edit here : XXXX + [number].gif
Public Const OUTPUT_FILE_PREFIX As String = "sec-"
' edit here : path to ImageMagick
Public Const IMAGE_MAGICK As String = """C:\Program Files\ImageMagick-7.1.1-Q16-HDRI\convert.exe"""

' do not need to edit
Public Const IMAGE_MAGICK_PARAMS As String = "+dither -delay 100 -loop 0 -layers Optimize"
Public Const TMPDIR_PREFIX As String = ".tmp-"

Function moveToFilePath() As Boolean
	Dim path As String

	path = ActivePresentation.FullName
	If path <> "" Then
		ChDir Left(path, InStrRev(path, PATH_SEPARATOR))
		moveToFilePath = True
	Else
		moveToFilePath = False
	End If
End Function

Sub setUpFolder(in_folder As String)
	Dim fso As Object

	Set fso = CreateObject("Scripting.FileSystemObject")
	If fso.FolderExists(in_folder) Then
		Dim folder As Object
		Dim file As Object

		Set folder = fso.GetFolder(in_folder)
		For Each file In folder.Files
			file.Delete Force:=True
		Next file
		Set file = Nothing
		Set folder = Nothing
	Else
		fso.CreateFolder in_folder
	End If
	Set fso = Nothing
End Sub

Sub deleteFolder(in_folder As String)
	Dim fso As Object

	Set fso = CreateObject("Scripting.FileSystemObject")
	fso.deleteFolder in_folder, True
	Set fso = Nothing
End Sub

Sub convertImage(in_src_path As String, in_dst_path As String)
	Dim command(3) As String
	Dim shell As Object

	' build command
	command(0) = IMAGE_MAGICK
	command(1) = IMAGE_MAGICK_PARAMS
	command(2) = in_src_path
	command(3) = in_dst_path

	' exec command
	Set shell = CreateObject("WScript.Shell")
	shell.Run Join(command, " "), 0, True
	Set shell = Nothing
End Sub

Sub main()
	Dim pptx As Presentation
	Dim section As Integer

	If Not moveToFilePath Then
		Debug.Print "Error : there is not saved file"
		Exit Sub
	End If

	Set pptx = ActivePresentation
	For section = 1 To pptx.SectionProperties.Count
		Dim gif_path As String
		Dim tmp_path As String
		Dim ix_s As Integer
		Dim ix_e As Integer
		Dim page As Integer

		gif_path = "." & PATH_SEPARATOR & OUTPUT_FILE_PREFIX & Format(section, "000") & ".gif"
		If Dir(gif_path & ".*") <> "" Then
			Debug.Print "already exist : " & gif_path
		Else
			' initialize
			tmp_path = "." & PATH_SEPARATOR & TMPDIR_PREFIX & section
			setUpFolder tmp_path

			' pptx to PNG24
			ix_s = pptx.SectionProperties.FirstSlide(section)
			ix_e = ix_s + pptx.SectionProperties.SlidesCount(section) - 1
			For page = ix_s To ix_e
				pptx.Slides(page).Export tmp_path & PATH_SEPARATOR & page & ".png", "PNG", 1600
			Next page

			' PNG24 to Animation GIF
			convertImage tmp_path & PATH_SEPARATOR & "*.png", gif_path
			Debug.Print "created : " & gif_path

			' finalize
			deleteFolder tmp_path
		End If
	Next section
	Set pptx = Nothing
End Sub

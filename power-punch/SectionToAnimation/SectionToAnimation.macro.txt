' 1. open pptx
' 2. alt + F11
' 3. insert standard module
' 4. paste this script & edit it
' 5. exec macro (F5)

' edit here : depends on your system
Public Const PATH_SEPARATOR As String = "\"
' edit here : XXXX + [number].gif
Public Const OUTPUT_FILE_PREFIX As String = "sec-"
' edit here : path to ImageMagick
Public Const IMAGE_MAGICK As String = """C:\Program Files\ImageMagick-7.1.1-Q16-HDRI\convert.exe"""

' do not need to edit
Public Const IMAGE_MAGICK_PARAMS1 As String = "-delay 100 -loop 0 -layers Optimize"
Public Const IMAGE_MAGICK_PARAMS2 As String = "-kmeans 8"
Public Const TMPDIR_PREFIX As String = ".tmp-"

Function moveToFilePath() As Boolean
	Dim path As String

	path = ActivePresentation.FullName
	If path <> "" Then
		ChDir Left(path, InStrRev(path, PATH_SEPARATOR))
		moveToFilePath = True
	Else
		moveToFilePath = False
	End If
End Function

Sub setUpFolder(in_folder As String)
	Dim fso As Object

	Set fso = CreateObject("Scripting.FileSystemObject")
	If fso.FolderExists(in_folder) Then
		Dim folder As Object
		Dim file As Object

		Set folder = fso.GetFolder(in_folder)
		For Each file In folder.Files
			file.Delete Force:=True
		Next file
		Set file = Nothing
		Set folder = Nothing
	Else
		fso.CreateFolder in_folder
	End If
	Set fso = Nothing
End Sub

Sub deleteFolder(in_folder As String)
	Dim fso As Object

	Set fso = CreateObject("Scripting.FileSystemObject")
	fso.DeleteFolder in_folder, True
	Set fso = Nothing
End Sub

Function convertImage(in_dst As String, in_path As String) As String
	Dim fso As Object
	Dim folder As Object
	Dim file As Object
	Dim commandArray() As String
	Dim extension As String
	Dim ix As Integer

	' build command
	Set fso = CreateObject("Scripting.FileSystemObject")
	Set folder = fso.GetFolder(in_path)
	ReDim commandArray(folder.Files.Count - 1 + 3)
	commandArray(0) = IMAGE_MAGICK
	commandArray(1) = IMAGE_MAGICK_PARAMS1
	ix = 2
	For Each file In folder.Files
		commandArray(ix) = in_path & PATH_SEPARATOR & file.Name
		ix = ix + 1
	Next file
	Set file = Nothing
	Set folder = Nothing
	Set fso = Nothing

	If ix = 3 Then
		' this is not animation
		commandArray(1) = IMAGE_MAGICK_PARAMS2
		extension = ".png"
	Else
		extension = ".gif"
	End If
	commandArray(ix) = in_dst & extension

	' exec command
	Dim shell As Object
	Set shell = CreateObject("WScript.Shell")
	shell.Run Join(commandArray, " "), 0, True

	' return
	convertImage = extension
End Function

Sub main()
	Dim pptx As Presentation
	Dim section As Integer

	If Not moveToFilePath Then
		Debug.Print "Error : there is not saved file"
		Exit Sub
	End If

	Set pptx = ActivePresentation
	For section = 1 To pptx.SectionProperties.Count
		Dim ix_s As Integer
		Dim ix_e As Integer
		Dim in_src_folder As String
		Dim in_dst_file As String
		Dim page As Integer

		ix_s = pptx.SectionProperties.FirstSlide(section)
		ix_e = ix_s + pptx.SectionProperties.SlidesCount(section) - 1
		in_src_folder = "." & PATH_SEPARATOR & TMPDIR_PREFIX & section
		in_dst_file = "." & PATH_SEPARATOR & OUTPUT_FILE_PREFIX & Format(section, "000")

		If Dir(in_dst_file & ".*") <> "" Then
			Debug.Print "already exist : " & in_dst_file
		Else
			' initialize
			setUpFolder in_src_folder

			' pptx to PNG24
			For page = ix_s To ix_e
				pptx.Slides(page).Export in_src_folder & PATH_SEPARATOR & page & ".png", "PNG", 1600
			Next page

			' PNG24 to Animation GIF / PNG8
			Debug.Print "created : " & in_dst_file & convertImage(in_dst_file, in_src_folder)

			' finalize
			deleteFolder in_src_folder
		End If
	Next section
	Set pptx = Nothing
End Sub

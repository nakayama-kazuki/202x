' 1. open pptx
' 2. alt + F11
' 3. insert standard module
' 4. paste this script & edit it
' 5. exec macro (F5)

' edit here : depends on your system
Public Const PATH_SEPARATOR As String = "\"
' edit here : XXXX + [number].gif
Public Const OUTPUT_FILE_PREFIX As String = "sec-"
' edit here : path to ImageMagick
Public Const IMAGE_MAGICK As String = """C:\Program Files\ImageMagick-7.1.1-Q16-HDRI\convert.exe"""

' do not need to edit
Public Const IMAGE_MAGICK_PARAMS1 As String = "-delay 100 -loop 0 -layers Optimize"
Public Const IMAGE_MAGICK_PARAMS2 As String = "-kmeans 8"
Public Const TMPDIR_PREFIX As String = ".tmp-"

Function moveToFilePath() As Boolean
	Dim path As String

	path = ActivePresentation.FullName
	If path <> "" Then
		ChDir Left(path, InStrRev(path, PATH_SEPARATOR))
		moveToFilePath = True
	Else
		moveToFilePath = False
	End If
End Function

Sub setUpFolder(in_folder As String)
	Dim fso As Object

	Set fso = CreateObject("Scripting.FileSystemObject")
	If fso.FolderExists(in_folder) Then
		Dim folder As Object
		Dim file As Object

		Set folder = fso.GetFolder(in_folder)
		For Each file In folder.Files
			file.Delete Force:=True
		Next file
		Set file = Nothing
		Set folder = Nothing
	Else
		fso.CreateFolder in_folder
	End If
	Set fso = Nothing
End Sub

Sub deleteFolder(in_folder As String)
	Dim fso As Object

	Set fso = CreateObject("Scripting.FileSystemObject")
	fso.DeleteFolder in_folder, True
	Set fso = Nothing
End Sub

Function getFiles(in_folder As String) As Variant
	Dim fso As Object
	Dim folder As Object
	Dim file As Object
	Dim fileArray() As String
	Dim ix As Integer

	Set fso = CreateObject("Scripting.FileSystemObject")
	Set folder = fso.GetFolder(in_folder)
	ReDim fileArray(folder.Files.Count - 1)
	ix = 0
	For Each file In folder.Files
		fileArray(ix) = in_folder & PATH_SEPARATOR & file.Name
		ix = ix + 1
	Next file
	Set file = Nothing
	Set folder = Nothing
	Set fso = Nothing
	getFiles = fileArray
End Function

Sub convertImage(in_dst_file As String, in_folder As String)
	Dim fileArray As Variant
	Dim commandArray() As String
	Dim dst_file As String
	Dim ix As Integer
	Dim shell As Object

	' build command
	fileArray = getFiles(in_folder)
	ReDim commandArray(UBound(fileArray) - LBound(fileArray) + 1 + 3)
	commandArray(0) = IMAGE_MAGICK
	If UBound(fileArray) > 1 Then
		commandArray(1) = IMAGE_MAGICK_PARAMS1
		dst_file = in_dst_file & ".gif"
	Else
		' this is not animation
		commandArray(1) = IMAGE_MAGICK_PARAMS2
		dst_file = in_dst_file & ".png"
	End If
    For ix = LBound(fileArray) To UBound(fileArray)
		commandArray(2 + ix) = fileArray(ix)
    Next ix
	commandArray(UBound(commandArray)) = dst_file
	Debug.Print "creating : " & dst_file

	' exec command
	Set shell = CreateObject("WScript.Shell")
	shell.Run Join(commandArray, " "), 0, True
	Set shell = Nothing
End Sub

Sub main()
	Dim pptx As Presentation
	Dim section As Integer

	If Not moveToFilePath Then
		Debug.Print "Error : there is not saved file"
		Exit Sub
	End If

	Set pptx = ActivePresentation
	For section = 1 To pptx.SectionProperties.Count
		Dim dst_file As String
		Dim src_folder As String
		Dim ix_s As Integer
		Dim ix_e As Integer
		Dim page As Integer

		dst_file = "." & PATH_SEPARATOR & OUTPUT_FILE_PREFIX & Format(section, "000")
		If Dir(dst_file & ".*") <> "" Then
			Debug.Print "already exist : " & dst_file
		Else
			' initialize
			src_folder = "." & PATH_SEPARATOR & TMPDIR_PREFIX & section
			setUpFolder src_folder

			' pptx to PNG24
			ix_s = pptx.SectionProperties.FirstSlide(section)
			ix_e = ix_s + pptx.SectionProperties.SlidesCount(section) - 1
			For page = ix_s To ix_e
				pptx.Slides(page).Export src_folder & PATH_SEPARATOR & page & ".png", "PNG", 1600
			Next page

			' PNG24 to Animation GIF / PNG8
			convertImage dst_file, src_folder

			' finalize
			deleteFolder src_folder
		End If
	Next section
	Set pptx = Nothing
End Sub

' 1. open pptx
' 2. alt + F11
' 3. insert standard module
' 4. paste this script & edit it
' 5. exec macro (F5)

' edit here : depends on your system
Public Const PATH_SEPARATOR As String = "\"
' edit here : XXXX + [number].gif
Public Const OUTPUT_FILE_PREFIX As String = "sec-"
' edit here : path to ImageMagick
Public Const IMAGE_MAGICK As String = """C:\Program Files\ImageMagick-7.1.1-Q16-HDRI\convert.exe"""

' does not need to edit
Public Const IMAGE_MAGICK_PARAMS1 As String = "-delay 100 -loop 0 -layers Optimize"
Public Const IMAGE_MAGICK_PARAMS2 As String = "-kmeans 8"
Public Const TMPDIR_PREFIX As String = ".tmp-"

Sub moveToFilePath()
	Dim path As String
	path = ActivePresentation.FullName
	If path <> "" Then
		ChDir Left(path, InStrRev(path, PATH_SEPARATOR))
	Else
		MsgBox "There is not saved file"
	End If
End Sub

Function convertImage(in_dst As String, in_path As String) As String
	Dim fso As Object
	Dim folder As Object
	Dim file As Object
	Dim commandArray() As String
	Dim extension As String
	Dim ix As Integer

	Set fso = CreateObject("Scripting.FileSystemObject")
	Set folder = fso.GetFolder(in_path)

	' build command
	ReDim commandArray(folder.Files.Count - 1 + 3)
	commandArray(0) = IMAGE_MAGICK
	commandArray(1) = IMAGE_MAGICK_PARAMS1
	ix = 2
	For Each file In folder.Files
		commandArray(ix) = in_path & PATH_SEPARATOR & file.Name
		ix = ix + 1
	Next file
	If ix = 3 Then
		' only 1 file
		commandArray(1) = IMAGE_MAGICK_PARAMS2
		extension = ".png"
	Else
		extension = ".gif"
	End If
	commandArray(ix) = in_dst & extension

	' exec command
	Dim shell As Object
	Set shell = CreateObject("WScript.Shell")
	shell.Run Join(commandArray, " "), 0, True

	' return
	convertImage = extension
End Function

Sub main()
	Dim fso As Object
	Dim pptx As Presentation

	moveToFilePath
	Set fso = CreateObject("Scripting.FileSystemObject")
	Set pptx = ActivePresentation

	For section = 1 To pptx.SectionProperties.Count
		Dim ix_s As Integer
		Dim ix_e As Integer
		Dim src As String
		Dim dst As String
		Dim page As Integer

		ix_s = pptx.SectionProperties.FirstSlide(section)
		ix_e = ix_s + pptx.SectionProperties.SlidesCount(section) - 1
		src = "." & PATH_SEPARATOR & TMPDIR_PREFIX & section
		dst = "." & PATH_SEPARATOR & OUTPUT_FILE_PREFIX & Format(section, "000")

		If Dir(dst & ".*") <> "" Then
			Debug.Print "already exist : " & dst
		Else
			Dim extension As String

			' initialize
			If fso.FolderExists(src) Then
				Dim folder As Object
				Dim file As Object
				Set folder = fso.GetFolder(src)
				For Each file In folder.Files
					file.Delete Force:=True
				Next file
			Else
				fso.CreateFolder src
			End If

			' pptx to PNG24
			For page = ix_s To ix_e
				pptx.Slides(page).Export src & PATH_SEPARATOR & page & ".png", "PNG", 1600
			Next page

			' PNG24 to Animation GIF / PNG8
			extension = convertImage(dst, src)

			' finalize
			fso.DeleteFolder src, True
			Debug.Print "created : " & dst & extension
		End If
	Next section
End Sub

